buildscript {
    ext {
        kotlin_version = '1.4.10'
        rabbitmq_java_client_version = '5.11.0'
        junit_jupiter_version = '5.7.1'
        junit_plugin_version = '1.2.0'
        kluent_version = '1.48'
        ktor_version = '1.6.0'
        vertx_version = '4.0.2'
        testcontainers_version = '1.15.2'
        httpclient_version = '4.5.13'
        rabbitmq_http_client_version = '3.7.0.RELEASE'
    }

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        //maven { url 'https://repo.spring.io/snapshot' }
        //maven { url 'https://repo.spring.io/milestone' }
        jcenter()
    }

    dependencies {
        classpath "com.novoda:bintray-release:0.9.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:5.1.0"
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.5.3"
        classpath "org.jetbrains.kotlin:kotlin-allopen:1.4.31"
        classpath "com.github.jengelman.gradle.plugins:shadow:5.1.0"
        classpath 'com.adarshr:gradle-test-logger-plugin:2.1.1'
        //classpath("org.springframework.boot:spring-boot-gradle-plugin:2.2.6.RELEASE")
    }
}

plugins {
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

configure(allprojects) { project ->
    group = 'com.viartemev'

    apply plugin: "java"
    apply plugin: "kotlin"
    apply plugin: "idea"

    repositories {
        mavenCentral()
        jcenter()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    sourceCompatibility = 1.8

    dependencies {
        compile 'io.github.microutils:kotlin-logging:2.0.6'

        compile "com.rabbitmq:amqp-client:$rabbitmq_java_client_version"
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.3.7-mpp-dev-11"
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2-native-mt'
        testCompile 'org.slf4j:slf4j-api:2.0.0-alpha1'
        testRuntime 'ch.qos.logback:logback-classic:1.3.0-alpha5'
        testRuntime 'ch.qos.logback:logback-core:1.3.0-alpha5'
    }
}

configure(rootProject) {
    version = '0.0.6'

    apply plugin: "com.novoda.bintray-release"
    apply plugin: 'com.adarshr.test-logger'

    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled true
        }
    }

    dependencies {
        testImplementation "com.rabbitmq:http-client:$rabbitmq_http_client_version"
        testImplementation "org.springframework:spring-web:5.3.5"
        testImplementation "org.apache.httpcomponents:httpclient:$httpclient_version"
        testCompile "org.testcontainers:testcontainers:$testcontainers_version"
        testCompile "org.testcontainers:junit-jupiter:$testcontainers_version"
        testCompile "com.nhaarman:mockito-kotlin:1.6.0"
        testCompile "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"
    }

    publish {
        userOrg = 'viartemev'
        repoName = 'Maven'
        groupId = 'com.viartemev'
        artifactId = 'the-white-rabbit'
        publishVersion = '0.0.6'
        desc = 'The White Rabbit is an asynchronous RabbitMQ library based on Kotlin coroutines'
        website = 'https://github.com/viartemev/the-white-rabbit'
        licences = ['MIT']
    }
}

project('the-white-rabbit-ktor-example') {
    apply plugin: 'application'
    mainClassName = "io.ktor.server.netty.EngineMain"

    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://kotlin.bintray.com/ktor' }
    }

    sourceSets {
        main.kotlin.srcDirs = main.java.srcDirs = ['src']
        test.kotlin.srcDirs = test.java.srcDirs = ['test']
        main.resources.srcDirs = ['resources']
        test.resources.srcDirs = ['testresources']
    }

    dependencies {
        compile rootProject
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        compile "io.ktor:ktor-server-netty:$ktor_version"
        compile "ch.qos.logback:logback-classic:1.3.0-alpha5"
        compile "io.ktor:ktor-server-core:$ktor_version"
        compile "io.ktor:ktor-jackson:$ktor_version"
        testCompile "io.ktor:ktor-server-tests:$ktor_version"
    }

}

/*project('the-white-rabbit-spring-boot-example') {
    repositories {
        maven { url 'https://repo.spring.io/snapshot' }
        maven { url 'https://repo.spring.io/milestone' }
    }

    apply plugin: "org.jetbrains.kotlin.plugin.spring"
    apply plugin: org.springframework.boot.gradle.plugin.SpringBootPlugin
    apply plugin: "io.spring.dependency-management"

    test {
        useJUnitPlatform()
    }

    dependencies {
        compile rootProject
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3-native-mt'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude module: 'junit'
        }
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        testImplementation 'io.projectreactor:reactor-test'
    }
}*/

project('the-white-rabbit-vertx-example') {
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "application"

    mainClassName = 'io.vertx.core.Launcher'
    def mainVerticleName = 'com.viartemev.the_white_rabbit_vertx_example.App'

    shadowJar {
        classifier = 'fat'
        mergeServiceFiles {
            include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
        }
    }

    dependencies {
        compile rootProject
        compile "io.vertx:vertx-lang-kotlin:$vertx_version"
        compile "io.vertx:vertx-lang-kotlin-coroutines:$vertx_version"
        compile "io.vertx:vertx-web:$vertx_version"
    }
}

project('the-white-rabbit-example') {
    dependencies {
        compile rootProject
        compile group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0-alpha1'
        runtime group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha4'
        runtime group: 'ch.qos.logback', name: 'logback-core', version: '1.3.0-alpha4'
    }
}

project('benchmarks') {
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "me.champeau.gradle.jmh"

    jmh.jmhVersion = '1.21'

    jmhJar {
        baseName 'benchmarks'
        classifier = null
        version = null
        destinationDir = file("$rootDir")
    }

    dependencies {
        compile rootProject
        compile "org.openjdk.jmh:jmh-core:1.25.1"
        jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.29'
    }

    task deleteEmptyBmList(type: Delete) {
        delete "$buildDir/jmh-generated-classes/META-INF/BenchmarkList"
    }
    jmhCompileGeneratedClasses.finalizedBy deleteEmptyBmList
}

